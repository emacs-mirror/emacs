#!/bin/sh

### make-info-dir - create info/dir, for systems without install-info

## Copyright (C) 2013-2026 Free Software Foundation, Inc.

## Author: Glenn Morris <rgm@gnu.org>
## Maintainer: emacs-devel@gnu.org

## This file is part of GNU Emacs.

## GNU Emacs is free software: you can redistribute it and/or modify
## it under the terms of the GNU General Public License as published by
## the Free Software Foundation, either version 3 of the License, or
## (at your option) any later version.

## GNU Emacs is distributed in the hope that it will be useful,
## but WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
## GNU General Public License for more details.

## You should have received a copy of the GNU General Public License
## along with GNU Emacs.  If not, see <https://www.gnu.org/licenses/>.

### Commentary:

## Generate info/dir, for systems without install-info.
## Expects to be called from top-level Emacs source directory.

## It only handles the case where info/dir is missing from the
## installation directory.  It does not handle info/dir being present
## but missing some entries.

## Header contains non-printing characters, so this is more
## reliable than using awk.
test $# -ge 2 || exit 1
cat <"$1"
shift

exec "${AWK-awk}" '
  function detexinfo() {
    gsub(/@value\{emacsname\}/, "Emacs")
    gsub(/@[^{]*\{/, "")
    gsub(/}/, "")
  }
  BEGIN {
    ntopics = 0
    topic[ntopics++] = "Texinfo documentation system"
    topic[ntopics++] = "Emacs"
    topic[ntopics++] = "Emacs lisp"
    topic[ntopics++] = "Emacs editing modes"
    topic[ntopics++] = "Emacs network features"
    topic[ntopics++] = "Emacs misc features"
    topic[ntopics++] = "Emacs lisp libraries"
    topic[ntopics] = "Unknown category"
    texinfo = 0
  }

  /^@dircategory / {
    texinfo = 1
    sub(/^@dircategory /, "")
    detexinfo()
    for (dircat = 0; dircat < ntopics && topic[dircat] != $0; dircat++)
      continue;
  }
  /^@direntry/, /^@end direntry/ {
    # FIXME do not ignore w32 if OS is w32.
    if ($0 !~ /^@/ && $0 !~ /w32/) {
      detexinfo()
      data[dircat] = data[dircat] $0 "\n"
    }
  }

  ## Org stuff. TODO we assume the order of the texinfo items.
  {
    ## TODO Check FILENAME suffix instead?
    ## TODO Is this portable awk?
    if (FNR == 1) texinfo = 0

    ## If applied to the generated org.texi file, this picks up the examples.
    ## Thanks for making life more difficult...
    if (texinfo) next

    if (tolower($0) ~ /^#\+texinfo_dir_category/) {
      sub(/^#[^:]*: /, "")
      for (dircat = 0; dircat < ntopics && topic[dircat] != $0; dircat++)
        continue;
    }
    if (tolower($0) ~ /^#\+(texinfo_dir_title|texinfo_dir_name)/) {
      sub(/^#[^:]*: /, "")
      ## Note this does not fill any long descriptions.
      data[dircat] = data[dircat] sprintf("* %-30s", ($0 ". "))
    }
    if (tolower($0) ~ /^#\+texinfo_dir_desc/) {
      sub(/^#[^:]*: /, "")
      data[dircat] = data[dircat] $0 ".\n"
    }
  }

  END {
    for (dircat = 0; dircat <= ntopics; dircat++)
      if (data[dircat])
        printf "\n%s\n%s", topic[dircat], data[dircat]
  }
' "$@"
