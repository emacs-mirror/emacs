# -sed6.inp------------------------------------------------------------------
# Configuration script for doc/emacs/Makefile, doc/lispref/Makefile,
# doc/lispintro/Makefile, and doc/misc/Makefile under DJGPP v2.x
# ---------------------------------------------------------------------------
#
# Copyright (C) 1997, 2000-2018 Free Software Foundation, Inc.
#
# This file is part of GNU Emacs.
#
# This file is free software; as a special exception, the author gives
# unlimited permission to copy and/or distribute it, with or without
# modifications, as long as this notice is preserved.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
#
# ----------------------------------------------------------------------
/^srcdir *=/s/@[^@\n]*@/./
/^VPATH *=/s/@[^@\n]*@/./
/^MAKEINFO *=/s/@[^@\n]*@/makeinfo/
/^AM_DEFAULT_VERBOSITY *=/s/@AM_DEFAULT_VERBOSITY@/1/
/^[Aa][Mm]_/s/@AM_V@/$(V)/
/^[Aa][Mm]_/s/@AM_DEFAULT_V@/$(AM_DEFAULT_VERBOSITY)/
/^ENVADD/,/^$/c\
ENVADD =\
export TEXINPUTS := $(srcdir)";"$(texinfodir)";"$(TEXINPUTS)\
export MAKEINFO := $(MAKEINFO) $(MAKEINFO_OPTS)
/^SHELL *=/s/^/# /
/^doc-emacsver:/,/^$/ {
  s/emacsver\.texi\.in/emacsver.in/g
  s/\.texi\.\$\$\$\$/.tmp/g
  s/[^	 ]*move-if-change/update/
}
