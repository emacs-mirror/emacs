# -sedleim.inp-----------------------------------------------------------
# Configuration script for leim/Makefile under DJGPP v2.x
# ----------------------------------------------------------------------
#
# Copyright (C) 1999-2018 Free Software Foundation, Inc.
#
# This file is part of GNU Emacs.
#
# This file is free software; as a special exception, the author gives
# unlimited permission to copy and/or distribute it, with or without
# modifications, as long as this notice is preserved.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
#
# ----------------------------------------------------------------------
/^SHELL *=/c\
SHELL=/xyzzy/command\
MAKESHELL=/xyzzy/command

/^EXEEXT *=/s/@EXEEXT@/.exe/
/^AM_DEFAULT_VERBOSITY *=/s/@AM_DEFAULT_VERBOSITY@/1/
/^[Aa][Mm]_/s/@AM_V@/$(V)/
/^[Aa][Mm]_/s/@AM_DEFAULT_V@/$(AM_DEFAULT_VERBOSITY)/

/^srcdir=/s|srcdir=@srcdir@|srcdir := $(subst \\,/,$(shell cd))|

# Need a relative directory name for the md command, otherwise it
# chokes, perhaps due to multiple dots in emacs-XX.YY.NN name.
/^leimdir *=/a\
rel_leimdir = ..\\lisp\\leim

s|\([ 	]\)echo|\1djecho|g
/^	@true *$/d

/RUN_EMACS *=/,/^$/c\
export EMACSLOADPATH=\
RUN_EMACS = ${EMACS} -batch --no-site-file --no-site-lisp

/^MKDIR_P *=/s,@MKDIR_P@,gmkdir -p,

/^\${leimdir}\/quail \${leimdir}\/ja-dic: *$/s|\${leimdir}/|$(rel_leimdir)\\|
